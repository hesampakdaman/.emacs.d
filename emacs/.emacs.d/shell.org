* Introduction
Shell related packages and configurations. Our main emulator is
vterm. In order to get all environment variables correctly we define
them in `.bash_profile`, let the package `exec-path-from-shell` read
them and intialize our terminal correctly.

* Options
Use completions other than for binaries.
#+BEGIN_SRC emacs-lisp
  (setq shell-completion-execonly nil)
#+END_SRC

* exec-path-from-shell
Uses environment variables defined in your env files
correctly. Otherwise, we have executables that are not reconginzed by
Emacs shells.
#+BEGIN_SRC emacs-lisp
  (use-package exec-path-from-shell
    :config
    (exec-path-from-shell-initialize))
#+END_SRC

* shx
#+BEGIN_SRC emacs-lisp
  (use-package shx
    :init
    (add-hook 'shell-mode-hook  #'shx-mode))
#+END_SRC

* vterm
Powerful terminal emulator based on libvterm written in C. Fast,
colorful a delight.
#+BEGIN_SRC emacs-lisp
  (use-package vterm
    :demand t
    :after project
    :bind ("M-SPC v" . 'vterm-other-window)
    :config
    (define-key vterm-mode-map (kbd "M-SPC") nil)
    (add-to-list 'project-switch-commands '(hesam/vterm-project-shell "vterm" ?s))
    (advice-add #'project-shell :override #'hesam/vterm-project-shell))
#+END_SRC

The custom function `hesam/vterm-project-shell` is for usage with
`project.el`. It overrides the default shell. The function is rather
long because we want to make sure vterm is started in `project-root`
and have requirements on how and when a new window is created.
#+BEGIN_SRC emacs-lisp
  (defun hesam/vterm-project-shell (arg)
    "Run `vterm' in the current project's root.

  If the VTerm buffer for this project exists and is visible, switch to it.
  If it exists but is not visible, split the window below and switch to it.
  If the buffer does not exist, create a new one and switch to it.
  A numeric prefix argument ARG does two things:
  - If a buffer associated with the prefix exists, switch to that buffer accordingly.
  - Otherwise, create a new buffer with the prefix number in the name."
    (interactive "p")
    (if (fboundp 'project-root)
        (let* ((root (project-root (project-current t)))
               (dir-name (file-name-nondirectory (directory-file-name root)))
               (buffer-name (format "*vterm %s*%s" dir-name (if (> arg 1) (format "<%d>" arg) "")))
               (existing-buffer (get-buffer buffer-name)))
          (if existing-buffer
              (let ((buffer-window (get-buffer-window existing-buffer)))
                (if buffer-window
                    (select-window buffer-window)
                  (progn
                    (split-window-below)
                    (switch-to-buffer-other-window existing-buffer))))
            (let ((new-buffer (vterm-other-window buffer-name)))
              (with-current-buffer new-buffer
                (cd root)))))))
#+END_SRC

* xterm colors
Enables full color support in Emacs comint buffers.
#+BEGIN_SRC emacs-lisp
  (use-package xterm-color
    :config
    ;; Provides colors for comint buffers
    (setq comint-output-filter-functions
          (remove 'ansi-color-process-output comint-output-filter-functions))
    :hook
    (shell-mode . (lambda ()
                    ;; Disable font-locking in this buffer to improve performance
                    (font-lock-mode -1)
                    ;; Prevent font-locking from being re-enabled in this buffer
                    (make-local-variable 'font-lock-function)
                    (setq font-lock-function (lambda (_) nil))
                    (add-hook 'comint-preoutput-filter-functions 'xterm-color-filter nil t))))
#+END_SRC
