#+TITLE: Org mode settings
#+STARTUP: OVERVIEW
* Introduction
At this point we assume `Org` is installed and ready to go. This is
safe because in our `init.el` we use babel to load all modules and
therefore `Org` is required.
#+BEGIN_SRC emacs-lisp
  (require 'org)
#+END_SRC

Then just basically set up our org mode directory
#+BEGIN_SRC emacs-lisp
  (defcustom hesam-org-directory "~/Dropbox/org"
    "Default directory for where Org will look at put files."
    :type 'directory
    :group 'hesam)
  (setq org-directory hesam-org-directory)
#+END_SRC

Lastly, we want to display Org SRC code blocks in the same window
#+BEGIN_SRC emacs-lisp
  (setq org-src-window-setup 'current-window)
#+END_SRC

* Babel
What lanaguages to support for literal programming in Org buffers.
#+BEGIN_SRC emacs-lisp
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((C . t)
     (python . t)
     (shell . t)))
#+END_SRC

* Agenda and GTD
Agenda files are stored in Dropbox under `org/agenda`. If we put our
GTD file inside `org-agenda-files`, then any dates in the GTD file
will be seen by org agenda. We also define some nice tags to use so
that we can filter on them in the Agenda view.
#+BEGIN_SRC emacs-lisp
    (setq gtd-file (concat (eval org-directory) "/agenda/gtd.org"))
    (setq org-agenda-files `(,gtd-file)
          org-default-notes-file "~/Dropbox/org/notes.org"
          org-tag-alist '(("@work" . ?w) ("@home" . ?h)))
#+END_SRC

`org-outline-path-complete-in-steps` when set to nil allows selection
of the refile target using a single completion step, which will
provide a faster selection method if you have deeply nested
structures.
#+BEGIN_SRC emacs-lisp
  (setq org-outline-path-complete-in-steps t)
#+END_SRC

When refiling target is GTD file we want to automatically save the
buffer.
#+BEGIN_SRC emacs-lisp
  (defun hesam/org-refile-save (&rest _)
    "Save the buffer after refiling.

     This function checks if the current buffer is visiting 'gtd.org'.
     If so, it saves the buffer. It is used as an advice to `org-refile`,
     meaning it is called every time after `org-refile` is invoked."
    (when (eq (current-buffer) (find-buffer-visiting "gtd.org"))
      (save-buffer)))

  (advice-add 'org-refile :after 'hesam/org-refile-save)
#+END_SRC

Defines a toggler to show our GTD file whenever it is not visible in
the window and to hide it if it is.
#+BEGIN_SRC emacs-lisp
  (defun gtd/toggle-doing ()
    "Access the GTD gtd.org file inside `gtd-directory'.
    The tasks in this file are actively working on. If the buffer
    is visible then delete it, else open it in another window."
    (interactive)
    (let ((buf (get-buffer "gtd.org")))
      (if (and buf (get-buffer-window buf))
          (delete-window (get-buffer-window buf))
        (find-file-other-window gtd-file))))
#+END_SRC

** View
Start with current day and do not show tasks that are done.
#+BEGIN_SRC emacs-lisp
  (setq org-agenda-start-on-weekday nil
        org-agenda-skip-deadline-if-done t
        org-agenda-skip-scheduled-if-done t
        org-agenda-show-all-dates t)
#+END_SRC

From emacs cafe [[https://emacs.cafe/emacs/orgmode/gtd/2017/06/30/orgmode-gtd.html][blog]] to get gtd context filtered in the dispatcher.
#+BEGIN_SRC emacs-lisp
  (setq org-agenda-custom-commands
        '(("w" "Context work" tags-todo "@work"
           ((org-agenda-overriding-header "Work")
            (org-agenda-skip-function #'my-org-agenda-skip-all-siblings-but-first)))
          ("h" "Context home" tags-todo "@home"
           ((org-agenda-overriding-header "Home")
            (org-agenda-skip-function #'my-org-agenda-skip-all-siblings-but-first)))))

  (defun my-org-agenda-skip-all-siblings-but-first ()
    "Skip all but the first non-done entry."
    (let (should-skip-entry)
      (unless (org-current-is-todo)
        (setq should-skip-entry t))
      (save-excursion
        (while (and (not should-skip-entry) (org-goto-sibling t))
          (when (org-current-is-todo)
            (setq should-skip-entry t))))
      (when should-skip-entry
        (or (outline-next-heading)
            (goto-char (point-max))))))

  (defun org-current-is-todo ()
    (string= "TODO" (org-get-todo-state)))
#+END_SRC

And lastly to cleanup the Org agenda TODO view
#+BEGIN_SRC emacs-lisp
(setq org-agenda-prefix-format
      '((agenda . " %i %-12:c%?-12t% s")
        (todo   . " ")
        (tags   . " %i %-12:c")
        (search . " %i %-12:c")))
#+END_SRC

** Keybindings
For accessing Org agenda and GTD quickly.
#+BEGIN_SRC emacs-lisp
(leader-org-mode
    "a" 'org-agenda
    "c" 'org-capture
    "g" 'gtd/toggle-doing
    "l" 'org-store-link
    "x" 'org-archive-subtree)

  (leader-org-mode
    :keymaps 'org-mode-map
    "r" 'org-refile)
#+END_SRC

** Templates
Storage for Org capture is set to Dropbox to provide syncing between
devices.
#+BEGIN_SRC emacs-lisp
  (setq org-capture-templates
        '(("i" "Inbox" entry
           (file+headline gtd-file "Inbox") "* %u %i%?")
          ("p" "Planned" entry
           (file+headline gtd-file "Planned") "* %t %i%?")))
#+END_SRC

* Appearance
We basically use org-modern for look and feel. But using
`org-modern-star` screws with navigation `C-n/p`. We therefore don't
use it. [[https://github.com/minad/org-modern/issues/134][See this Github issue]].
#+BEGIN_SRC emacs-lisp
  (use-package org-modern
    :config (setq org-modern-star nil)
    :hook (org-mode . org-modern-mode))
#+END_SRC

So in order to get nice bullet points we use `org-bullets`.
#+BEGIN_SRC emacs-lisp
  (use-package org-bullets
    :hook (org-mode . org-bullets-mode))
#+END_SRC
